apply plugin: 'maven-publish'

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

publishing {
    publications {
        "$ARTIFACT_ID"(MavenPublication) {
            groupId project.GROUP_ID
            artifactId project.ARTIFACT_ID
            version project.VERSION
            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/$ARTIFACT_ID-release.aar")

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate implementation dependencies
                configurations.implementation.allDependencies.each {
                    if (it.group != null) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
                // Iterate releaseImplementation dependencies
                configurations.releaseImplementation.allDependencies.each {
                    if (it.group != null) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name 'release'
            url 'https://oss.sonatype.org/content/repositories/releases/'
            credentials {
                username = properties.getProperty("oss.usr")
                password = properties.getProperty("oss.pwd")
            }
        }

        maven {
            name 'snapshot'
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
            credentials {
                username = properties.getProperty("oss.usr")
                password = properties.getProperty("oss.pwd")
            }
        }
    }
}

